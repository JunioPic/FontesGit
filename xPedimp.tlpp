#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TOPCONN.CH"



User Function xPedimp()

  Local oDlg
  Local oGet
  Local cTitulo := " Impressão Pedido"
  Local cPedido := Space(6)

   
 DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 200,200 PIXEL STYLE 128

 @ 020,035 Say oSay Var 'Nº Pedido:' SIZE 100,100 OF oDlg PIXEL 
 @ 030,030 Get oGet Var cPedido PICTURE "999999" SIZE 40,15 OF oDlg PIXEL
 

 @ 080,010  BUTTON oBtn PROMPT "Imprimir" SIZE 35,15 ACTION xRelimp(cPedido)  OF oDlg PIXEL
 @ 080,060  BUTTON oBtn1 PROMPT "Fechar"   SIZE 35,15 ACTION (oDlg:End()) OF oDlg  PIXEL

 ACTIVATE MSDIALOG oDlg CENTERED
    
Return 

Static Function xRelimp(cPedido)

    Local cQuery   := ""
    Local _nVltotalped := 0
    
    Private oPrinter
    Private oFont1
    Private oFont2
    Private oHGRAY := TBrush():New( , CLR_HGRAY)
    Private lPreview
    Private cLogo := FisxLogo("1")
    Private nLin := 50
    Private nLinF := 585
    Private nNext := 0
    Private dEmissao := ""
    Private dData    := Dtoc(dDatabase)
    Private hHora    := Time()
    Private cPedCom  := cPedido
    Private _cMoeda  := ""
    Private cMensg01 := GETMV("MV_XMIMP01")
    Private cMensg02 := GETMV("MV_XMIMP02")
    Private cMensg03 := GETMV("MV_XMIMP03")
    Private cMensg04 := GETMV("MV_XMIMP04")
    Private cMensg05 := GETMV("MV_XMIMP05")
    Private cMensg06 := GETMV("MV_XMIMP06")
   
    
	    /*** FONTES ***/
	Private oFont18T  	:= TFont():New("Courier New",,18,,.T.,,,,,.F.)
	Private oFont16T  	:= TFont():New("Courier New",,16,,.T.,,,,,.F.)
	Private oFont14TC 	:= TFont():New("Courier New",,14,,.T.,,,,,.F.)
	Private oFont14T  	:= TFont():New("Arial"      ,,14,,.T.,,,,,.F.)
	Private oFont18TA 	:= TFont():New("Arial"      ,,18,,.T.,,,,,.F.)
	Private oFont14TI  	:= TFont():New("Arial"      ,,13,,.T.,,,,.T.,.F.)
	Private oFont11F  	:= TFont():New("Arial"      ,,11,,.F.,,,,,.F.)
	Private oFont13T  	:= TFont():New("Arial"      ,,12,,.T.,,,,,.F.)
	Private oFont13F  	:= TFont():New("Arial"      ,,12,,.F.,,,,,.F.)
	Private oFont10FA 	:= TFont():New("Arial"      ,,10,,.F.,,,,,.F.)
	Private oFont10F 	:= TFont():New("Courier New",,10,,.F.,,,,,.F.)
	Private oFont11FA 	:= TFont():New("Arial"      ,,11,,.F.,,,,,.F.)
	
	Private oFont09T  	:= TFont():New("Courier New",,09,,.T.,,,,,.F.)
	Private oFont07F  	:= TFont():New("Courier New",,07,,.F.,,,,,.F.)
	Private oFont09F  	:= TFont():New("Courier New",,09,,.F.,,,,,.F.)
	Private oFont09FA  	:= TFont():New("Arial",,09,,.F.,,,,,.F.)
	
	Private oFont7TA  	:= TFont():New("Courier New",,07,,.T.,,,,,.F.)
	Private oFont12F  	:= TFont():New("Courier New",,12,,.F.,,,,,.F.)
	Private oFont10T  	:= TFont():New("Courier New",,10,,.T.,,,,,.F.)
	Private oFont14N  	:= TFont():New("Courier New",14,14,,.T.,,,,.T.,.F.)
	
	Private oFont10AT  	:= TFont():New("Arial"      ,,10,,.T.,,,,,.F.)
	Private oFont10AF  	:= TFont():New("Arial"      ,,10,,.F.,,,,,.F.)
	
    Private  NSpace10   := 10
    Private  NSpace20   := 20
    Private  NSpace30   := 30
    Private  NSpace40   := 40
    Private  NSpace50   := 50
	


  If oPrinter == Nil
    lPreview := .T.
    oPrinter := FWMSPrinter():New(cPedCom ,6,.F.,,.T.)
    oPrinter:SetResolution(72) //Tamanho estipulado para a Danfe
    oPrinter:SetLandScape() 
    oPrinter:SetPaperSize(9)
    oPrinter:SetMargin(005,005,005,005)
    oPrinter:cPathPDF :="C:\TEMP\"    
    EndIf
    
     cQuery := " SELECT DISTINCT                                                       " + CRLF
     cQuery += "  SW2.W2_PO_DT                                                         " + CRLF
     cQuery += " ,SW2.W2_NR_PRO                                                        " + CRLF
     cQuery += " ,CONVERT(VARCHAR(12), CAST(SW2.W2_DT_PRO AS DATE),113) AS DTPROFORMA  " + CRLF
     cQuery += " ,SW2.W2_PO_NUM                                              " + CRLF
     cQuery += " ,SW2.W2_INCOTER                                             " + CRLF
     cQuery += " ,SYQ.YQ_DESCR                                               " + CRLF
     cQuery += " ,SY9.Y9_DESCR                                               " + CRLF
     cQuery += " ,SY4.Y4_NOME                                                " + CRLF
     cQuery += " ,CONCAT(SW2.W2_FORN,' ','-',' ', SA2.A2_NOME) AS FORNECEDOR   " + CRLF
     cQuery += " ,SA2.A2_END                                                   " + CRLF
     cQuery += " ,SA2.A2_CEP                                                   " + CRLF
     cQuery += " ,SA2.A2_BAIRRO                                                " + CRLF
     cQuery += " ,SA2.A2_MUN                                                   " + CRLF
     cQuery += " ,SA2.A2_PAISORI                                               " + CRLF
     cQuery += " ,SA2.A2_CONTATO                                               " + CRLF
     cQuery += " ,CONCAT(TRIM(SA2.A2_DDI),'-',TRIM(SA2.A2_DDD),'-',SA2.A2_TEL) AS TEL " + CRLF
     cQuery += " ,SA2.A2_EMAIL                                                 " + CRLF
     cQuery += " ,SY1.Y1_NOME                                                  " + CRLF
     cQuery += " ,SY1.Y1_EMAIL                                                 " + CRLF
     cQuery += " ,CONCAT(SW2.W2_MOEDA,' ','/', ' ',SYE.YE_VLCON_C) AS MOEDA    " + CRLF
    if cEmpAnt == '01'
      cQuery += " ,SY6.Y6_XDECRI                                               " + CRLF  
    else
      cQuery += " ,SY6.Y6_DESCRI                                               " + CRLF  
    endif
     cQuery += " ,SW3.W3_POSICAO                                               " + CRLF
     cQuery += " ,SW3.W3_COD_I                                                 " + CRLF 
     cQuery += " ,SB1.B1_DESC                                                  " + CRLF
     cQuery += " ,SW3.W3_TEC                                                   " + CRLF
     cQuery += " ,SW3.W3_QTDE                                                  " + CRLF
     cQuery += " ,SB1.B1_UM                                                    " + CRLF
     cQuery += " ,SW3.W3_PRECO                                                 " + CRLF
     cQuery += " ,CONVERT(VARCHAR(12), CAST(SW3.W3_DT_EMB AS DATE),113) AS DTEMBARQUE    " + CRLF
     cQuery += " ,SW2.W2_NR_ALTE                                               " + CRLF
     cQuery += " ,CONVERT(VARCHAR(12), CAST(SW2.W2_DT_ALTE AS DATE),113) AS DTALTERACAO  " + CRLF
     cQuery += " FROM " +RetSqlName("SW2")+ " SW2(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SA2")+ " SA2(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SW3")+ " SW3(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SY1")+ " SY1(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SYE")+ " SYE(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SB1")+ " SB1(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SY6")+ " SY6(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SYQ")+ " SYQ(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SY4")+ " SY4(NOLOCK), " + CRLF
     cQuery += "      " +RetSqlName("SY9")+ " SY9(NOLOCK)  " + CRLF
     cQuery += " WHERE  SW2.W2_PO_NUM = '" + cPedCom + "'  " + CRLF
     cQuery += " AND SW3.W3_TEC <> ''                      " + CRLF
    iF cEmpAnt == '01'
      cQuery += " AND SY6.Y6_XDECRI <> ''                   " + CRLF
    else
      cQuery += " AND SY6.Y6_DESCRI <> ''                   " + CRLF  
    EndIf     
     cQuery += " AND SW2.W2_FILIAL = '" + xFilial ("SW2") + "'" + CRLF
     cQuery += " AND SW3.W3_FILIAL = '" + xFilial ("SW3") + "'" + CRLF
     cQuery += " AND SB1.B1_FILIAL = '" + xFilial ("SB1") + "'" + CRLF
     cQuery += " AND SA2.A2_COD = SW2.W2_FORN                 " + CRLF
     cQuery += " AND SW3.W3_PO_NUM = SW2.W2_PO_NUM            " + CRLF
     cQuery += " AND SY1.Y1_COD = SW2.W2_COMPRA               " + CRLF
     cQuery += " AND SYE.YE_MOEDA = SW2.W2_MOEDA AND SW2.W2_PO_DT = SYE.YE_DATA " + CRLF
     cQuery += " AND SB1.B1_COD = SW3.W3_COD_I                " + CRLF
     cQuery += " AND SY6.Y6_COD = SW2.W2_COND_PA              " + CRLF
     cQuery += " AND SYQ.YQ_VIA = SW2.W2_TIPO_EM              " + CRLF
     cQuery += " AND SY4.Y4_COD = SW2.W2_AGENTE               " + CRLF
     cQuery += " AND SY9.Y9_SIGLA = SW2.W2_DEST               " + CRLF
     cQuery += " AND SW2.D_E_L_E_T_ = ' '                     " + CRLF
     cQuery += " AND SW3.D_E_L_E_T_ = ' '                     " + CRLF
     cQuery += " AND SY1.D_E_L_E_T_ = ' '                     " + CRLF
     cQuery += " AND SYE.D_E_L_E_T_ = ' '                     " + CRLF
     cQuery += " ORDER BY SW3.W3_POSICAO                      " + CRLF
     
    If Select("QPO") > 0
        Dbselectarea("QPO")
        QPO->(DbClosearea())        
    EndIf
    
    TcQuery cQuery New Alias "QPO"

    TCSetField( 'QPO', "W2_PO_DT", "D" )

    dEmissao := Dtoc(QPO->W2_PO_DT)

     oPrinter:StartPage()
     oPrinter:Box (030, 015, 595, 825)
     xCadEmp(dEmissao)
    

   If   QPO->W2_NR_ALTE != 0
    oPrinter:Say(090, 550, "REVISION:",                          oFont10T)
    oPrinter:Say(090, 600, +TRANSFORM(QPO->W2_NR_ALTE, "@E 9"),  oFont10F)
    oPrinter:Say(100, 570, "DATE:",                              oFont10T)
    oPrinter:Say(100, 600, +ALLTRIM(QPO->DTALTERACAO),           oFont10F)
   EndIf 

    nLin += 10
     oPrinter:Line( nLin, 015, nLin, 825)
    
    nLin += 10
    oPrinter:Say(nLin, 330, "SUPPLIER'S DATA",                                         oFont14TC)
    nLin += 10                                                                                     
    oPrinter:Say(nLin, 030, "SUPPLIER: ",                                              oFont10T)
    oPrinter:Say(nLin, 075, +ALLTRIM(QPO->FORNECEDOR),                                 oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "ADDRESS: ",                 oFont10T)
    oPrinter:Say(nLin, 075, +ALLTRIM(QPO->A2_END),       oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "DISTRICT: ",                oFont10T)
    oPrinter:Say(nLin, 075, +ALLTRIM(QPO->A2_BAIRRO),    oFont10F)
    oPrinter:Say(nLin, 320, "CEP: ",                     oFont10T)
    oPrinter:Say(nLin, 340, +ALLTRIM(QPO->A2_CEP),       oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "CITY: ",                    oFont10T)
    oPrinter:Say(nLin, 075, +ALLTRIM(QPO->A2_MUN),       oFont10F)
    oPrinter:Say(nLin, 320, "COUNTRY: ",                 oFont10T)
    oPrinter:Say(nLin, 360, +ALLTRIM(QPO->A2_PAISORI),   oFont10F) 
    nLin += 10
    oPrinter:Say(nLin, 030, "CONTACT: ",                 oFont10T)
    oPrinter:Say(nLin, 075, +ALLTRIM(QPO->A2_CONTATO),   oFont10F)
    oPrinter:Say(nLin, 200, "PHONE: ",                   oFont10T)
    oPrinter:Say(nLin, 230, '('+ALLTRIM(QPO->TEL),       oFont10F)
    oPrinter:Say(nLin, 320, "E-MAIL: ",                  oFont10T)
    oPrinter:Say(nLin, 355, +ALLTRIM(QPO->A2_EMAIL),     oFont10F)
    
    nLin += 10
     oPrinter:Line( nLin, 015, nLin, 825)
    nLin += 10
    oPrinter:Say(nLin, 330, "BUYER'S DATA",               oFont14TC)
    nLin += 10
    oPrinter:Say(nLin, 030, "BUYER:",                     oFont10T)
    oPrinter:Say(nLin, 065, +ALLTRIM(QPO->Y1_NOME),       oFont10F)
    oPrinter:Say(nLin, 300, "E-MAIL:",                    oFont10T)
    oPrinter:Say(nLin, 340, +ALLTRIM(QPO->Y1_EMAIL),      oFont10F)
    nLin += 10
    oPrinter:Line( nLin, 015, nLin, 825)
    nLin += 10
    oPrinter:Say(nLin, 030, "PROFORMA INVOICE:",           oFont10T)
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->W2_NR_PRO),      oFont10F)
    oPrinter:Say(nLin, 350, "DATE:",                       oFont10T)
    oPrinter:Say(nLin, 380, +ALLTRIM(DTPROFORMA),          oFont10F)
    nLin += 10
    oPrinter:Line( nLin, 015, nLin, 825)
    nLin += 10
    oPrinter:Say(nLin, 030, "INCOTERMS:",                  oFont10T)
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->W2_INCOTER),     oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "MODE OF DELIVERY:",           oFont10T)
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->YQ_DESCR),       oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "DESTINATION:",                oFont10T)
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->Y9_DESCR),       oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "FORWARDER:",                 oFont10T)
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->Y4_NOME),       oFont10F)
    nLin += 10
    oPrinter:Say(nLin, 030, "TERMS OF PAYMENT:",          oFont10T)
 if cEmpAnt == '01'
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->Y6_XDECRI),     oFont10F)
 else
    oPrinter:Say(nLin, 120, +ALLTRIM(QPO->Y6_DESCRI),     oFont10F)
 endif
   nLin += 10
   oPrinter:Say(nLin, 030, "ORDER CURRENCY:",             oFont10T)
   oPrinter:Say(nLin, 120, +ALLTRIM(QPO->MOEDA),          oFont10F)
   nLin += 10
   oPrinter:Line( nLin, 015, nLin, 825)
   nLin += 10
    oPrinter:Say(nLin, 350, "ORDER ITEMS",                   oFont14TC)
   nLin += 10
    
    oPrinter:FillRect({nLin,  015, nLin+015, 825}, oHGRAY)
    oPrinter:Line( nLin, 015, nLin, 825)
    oPrinter:Line( nLin+015,015 ,nLin+015, 825)
    oPrinter:Line( nLin,015 ,nLin+015, 015)
    oPrinter:Line( nLin,060 ,nLin+015, 060)
    oPrinter:Line( nLin,380 ,nLin+015, 380)
    oPrinter:Line( nLin,450 ,nLin+015, 450)
    oPrinter:Line( nLin,520 ,nLin+015, 520)
    oPrinter:Line( nLin,550 ,nLin+015, 550)
    oPrinter:Line( nLin,630 ,nLin+015, 630)
    oPrinter:Line( nLin,730 ,nLin+015, 730)
    oPrinter:Line(nLin,825 ,nLin+020, 825)
    
    nLin += 10
    oPrinter:Say(nLin, 025, "ITEMS",                        oFont10T)
    oPrinter:Say(nLin, 160, "DESCRIPTION",                  oFont10T)
    oPrinter:Say(nLin, 405, "N.C.M.",                       oFont10T)
    oPrinter:Say(nLin, 470, "QUANTITY.",                    oFont10T)
    oPrinter:Say(nLin, 525, "UNIT.",                        oFont10T)
    oPrinter:Say(nLin, 565, "UNIT PRICE",                   oFont10T)
    oPrinter:Say(nLin, 650, "REQ. SHIP DATE",               oFont10T)
    oPrinter:Say(nLin, 770, "AMOUNT",                       oFont10T)                                                                                            
    
    nLin += 05
    While !("QPO")->(EOF())

    oPrinter:Line( nLin,060 ,nLin+015, 060)
    oPrinter:Line( nLin,380 ,nLin+015, 380)
    oPrinter:Line( nLin,450 ,nLin+015, 450)
    oPrinter:Line( nLin,520 ,nLin+015, 520)
    oPrinter:Line( nLin,550 ,nLin+015, 550)
    oPrinter:Line( nLin,630 ,nLin+015, 630)
    oPrinter:Line( nLin,730 ,nLin+015, 730)
    
    nLin += 15
    oPrinter:Line(nLin,015 ,nLin, 825)
    oPrinter:Say( nLin - 4, 030, +ALLTRIM(QPO->W3_POSICAO),                             oFont10F)
    oPrinter:Say( nLin - 4, 065, +ALLTRIM(QPO->B1_DESC),                                oFont10F)
    oPrinter:Say( nLin - 4, 390, +ALLTRIM(QPO->W3_TEC),                                 oFont10F)
    oPrinter:Say( nLin - 4, 460, +TRANSFORM(QPO->W3_QTDE,'@e 999,999.99'),              oFont10F)
    oPrinter:Say( nLin - 4, 530, +ALLTRIM(QPO->B1_UM),                                  oFont10F)
    oPrinter:Say( nLin - 4, 540, +TRANSFORM(QPO->W3_PRECO,'@E 999,999.999'),            oFont10F)
    oPrinter:Say( nLin - 4, 660, +ALLTRIM(DTEMBARQUE),                                  oFont10F)

    _cQtda := QPO->W3_QTDE
    _VlUnit := QPO->W3_PRECO
    _Vltotal := _VlUnit * _cQtda
    _cMoeda := SubStr(QPO->MOEDA, 1, 3)

    oPrinter:Say( nLin - 4, 745, +CValToChar(TRANSFORM(_Vltotal, '@E 999,999.999')), oFont10F)

    _nVltotalped += _Vltotal

    If nNext = 0
       nNext := nNext + 1 
       oPrinter:Say(040,  700, "PAGE:",                          oFont07F)
       oPrinter:Say(040,  730, +TRANSFORM(nNext, '@e 999'),     oFont07F)
    EndIf

     xNewpag()
     QPO->(DbSkip())
   EndDo

   xRodimp(_nVltotalped,_cMoeda)

  	If lPreview
	     oPrinter:Preview()
	EndIf                      

	FreeObj(oPrinter)
	oPrinter := Nil

Return

Static Function xCadEmp(dEmissao)

    Local aSM0Data2 := {}

   aSM0Data2 := FWSM0Util():GetSM0Data()
   _cEmp       := ALLTRIM(aSM0Data2[5][2])
   _cEmpTel    := ALLTRIM(aSM0Data2[6][2])
   _cEmpCnpj   := ALLTRIM(aSM0Data2[10][2])
   _cEmpIE     := ALLTRIM(aSM0Data2[12][2])
   _cEmpEnd    := ALLTRIM(aSM0Data2[14][2])
   _cEmpBairro := ALLTRIM(aSM0Data2[16][2])
   _cEmpCidade := ALLTRIM(aSM0Data2[17][2])
   _cEmpUf     := ALLTRIM(aSM0Data2[18][2])
   _cEmpCep    := ALLTRIM(aSM0Data2[19][2])

    oPrinter:SayBitmap( 040, 040, cLogo , 100, 50)
    oPrinter:Say(nLin,150,ALLTRIM(_cEmp),                                           oFont14TC)
    nLin += 10                                      
    oPrinter:Say(nLin,150,_cEmpEnd+"-"+_cEmpBairro+"-"+_cEmpCidade+"-"+_cEmpUf+"- CEP:"+TRANSFORM(_cEmpCep,"@R 99999-999"),oFont09F)
    oPrinter:Say(nLin,700, "DATE:",                                      oFont07F)
    oPrinter:Say(nLin,730, OemToAnsi(dData),                             oFont07F)
    nLin += 10
    oPrinter:Say(nLin,150,TRANSFORM(_cEmpTel, "@R (99) 9999-9999" )+"- CNPJ:"+TRANSFORM(_cEmpCnpj, "@R 99.999.999/9999-99")+"- I.E:"+TRANSFORM(_cEmpIE, "@R 999.999.999.999" ),  oFont09F)
    oPrinter:Say(nLin,700, "HOUR:",                                      oFont07F)
    oPrinter:Say(nLin,730, OemToAnsi(hHora),                             oFont07F)
     nLin += 15
    oPrinter:Say(nLin,150, "PURCHASE ORDER NR:" +ALLTRIM(cPedCom), oFont14TC)
    oPrinter:Say(nLin,700, "DATE: " +OemToAnsi(dEmissao), oFont12F)                   
Return

Static Function xRodimp(_nVltotalped,_cMoeda)

    nLin += 12
    oPrinter:Say(nLin, 030, "TOTAL ORDER AMOUNT: ",                      oFont14TC)
    oPrinter:Say(nLin, 150, +TRANSFORM(_nVltotalped,'@E 999,999.99'),    oFont14TC) 
    oPrinter:Say(nLin, 230, +ALLTRIM(_cMoeda),    oFont14TC)
    nLin += 10
    oPrinter:Line( nLin, 015, nLin, 825)
    nLin += 15
    oPrinter:Say(nLin, 330, " REMARKS ",                  oFont14TC)
    nLin += 15
    oPrinter:Say(nLin, 030, cMensg01, oFont10T)
    nLin += 10
    oPrinter:Say(nLin, 030, cMensg02, oFont10T)
    nLin += 10
    oPrinter:Say(nLin, 030, cMensg03, oFont10T)
    nLin += 10
    oPrinter:Say(nLin, 030, cMensg04, oFont10T)
    xNewPag()
    nLin += 10
    oPrinter:Say(nLin, 030, cMensg05, oFont10T)
    nLin += 10
    oPrinter:Say(nLin, 030, cMensg06, oFont10T)

Return

Static Function xNewPag()

 iF nLin >= nLinF -20
         oPrinter:EndPage()
         oPrinter:StartPage()
         oPrinter:Box (030, 015, 595, 825)
         nLin := 35
      nNext += 1 
         oPrinter:Say(020,  700, "PAG:",                          oFont07F)
         oPrinter:Say(020,  730, +TRANSFORM(nNext, '@e 999'),     oFont07F)
                
   EndIf

Return

